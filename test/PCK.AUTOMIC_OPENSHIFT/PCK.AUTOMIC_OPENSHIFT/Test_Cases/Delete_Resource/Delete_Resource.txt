*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        delete_resource
Test Template     Delete Resource Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ResourceType            ResourceName            AdvancedOptions        ProjectName
Delete Resource action with valid values passed in all fields and selector --all is not passed as Advanced Option
                      [Tags]    main_scenarios
                      OK        ${Delete_Resrc_Type}    ${Delete_Resrc_Name}    ${Advanced_Options}    ${Project_Name}

Delete Resource action when user passes non-existent Resource Name but advanced option label as ignore if not found
                      [Tags]    main_scenarios
                      OK        ${Delete_Resrc_Type}    ${Delete_Resrc_Name}    --ignore-not-found     ${Project_Name}

Delete Resource action with valid values passed only in mandatory field Resource Type
                      [Tags]    main_scenarios
                      OK        ${Delete_Resrc_Type}    ${EMPTY}                ${EMPTY}               ${EMPTY}

Delete Resource action with more than one Resource Type sent as input
                      OK        bc,dc                   ${EMPTY}                ${EMPTY}               ${EMPTY}

Mandatory assertion test for Delete Resource action when Resource Type is not provided
                      NOK       ${EMPTY}                ${Delete_Resrc_Name}    ${Advanced_Options}    ${Project_Name}

Delete Resource action with valid values passed in all fields and selector --all is passed as Advanced Option
                      NOK       ${Delete_Resrc_Type}    ${Delete_Resrc_Name}    --all                  ${EMPTY}

Delete Resource action when user passes invalid Resource Type as input
                      NOK       Invalid                 ${Delete_Resrc_Name}    ${Advanced_Options}    ${Project_Name}

Delete Resource action with invalid Advanced Options as input
                      NOK       ${Delete_Resrc_Type}    Invalid                 ${Advanced_Options}    ${Project_Name}

Delete Resource action with invalid Resource Name as input
                      NOK       ${Delete_Resrc_Type}    ${Delete_Resrc_Name}    Invalid                ${Project_Name}

Delete Resource action with invalid/non-existent Project Name as input
                      NOK       ${Delete_Resrc_Type}    ${Delete_Resrc_Name}    ${Advanced_Options}    Invalid

*** Keywords ***
Startup
    Log    *******Start Test "DELETE APPLICATION"*******
    Log    *******Connect to AE*******
    Connect AE

Delete Resource Template
    [Arguments]    ${OK_NOK}    ${ResourceType}    ${ResourceName}    ${AdvancedOptions}    ${ProjectName}
    [Documentation]    This test deletes all or a specified resource as provided by user
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ResourceType}: This field specifies the type of resource user wants to delete
    ...    -${ResourceName}: This field specifies the name of the resource type
    ...    -${AdvancedOptions}: This field specifies any advanced options which will be used with the delete resource action, this can be a label,selector or output file type
    ...    -${ProjectName}: This field specifies the project name in which the action has to be performed
    Generate String
    Init Action    ${DELETE_RESOURCE}
    Connect To OpenShift
    Run Keyword If    '${ResourceType}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_RESOURCE_TYPE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_RESOURCE_TYPE#    ${ResourceType}
    Run Keyword If    '${ResourceName}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_RESOURCE_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_RESOURCE_NAME#    ${ResourceName}
    Run Keyword If    '${AdvancedOptions}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_ADV_OPTS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_ADV_OPTS#    ${AdvancedOptions}
    Run Keyword If    '${ProjectName}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_PROJECT#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_PROJECT#    ${ProjectName}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "DELETE APPLICATION"*******
