*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        set_env_variable
Test Template     Set Environment Variable Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ResourceType        ResourceName        KeyValuePair                ProjectName        AdvancedOption
Set Environment Variable action with valid values passed in all fields for resource type buildconfig
                      [Tags]    main_scenarios
                      OK        buildconfig         ${Resource_Name}    ${Single_Key_Value_Pair}    ${Project_Name}    ${Srvc_Advanced_Options}

Set Environment Variable action with valid values passed in all fields for resource type deploymentconfig
                      [Tags]    main_scenarios
                      OK        deploymentconfig    ${Resource_Name}    ${Single_Key_Value_Pair}    ${Project_Name}    ${EMPTY}

Set Environment Variable action with key-value pair where input value contains space and special characters
                      OK        buildconfig         ${Resource_Name}    key1=test demo!@#space      ${EMPTY}           ${EMPTY}

Mandatory assertion test for Set Environment Variable when ResourceName is not provided
                      NOK       buildconfig         ${EMPTY}            ${Single_Key_Value_Pair}    ${EMPTY}           ${EMPTY}

Mandatory assertion test for Set Environment Variable when Key value pair is not provided
                      NOK       buildconfig         ${Resource_Name}    ${EMPTY}                    ${EMPTY}           ${EMPTY}

Set Environment Variable action with Invalid Resource name
                      NOK       deploymentconfig    Invalid             ${Single_Key_Value_Pair}    ${EMPTY}           ${EMPTY}

Set Environment Variable action with single Key-Value pair as input in incorrect format
                      NOK       deploymentconfig    ${Resource_Name}    Key-1=value1                ${EMPTY}           ${EMPTY}

Set Environment Variable action with Invalid Project
                      NOK       buildconfig         ${Resource_Name}    ${Single_Key_Value_Pair}    Invalid            ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "SET ENVIRONMENT VARIABLE"*******
    Log    *******Connect to AE*******
    Connect AE

Set Environment Variable Template
    [Arguments]    ${OK_NOK}    ${ResourceType}    ${ResourceName}    ${KeyValuePair}    ${ProjectName}    ${AdvancedOption}
    [Documentation]    This test sets environment variable.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ResourceType}: This field specifies resource type for which variable/s will be create or updated
    ...    -${ResourceName}: This field specifies the name of selected resource type in which environment variable/s will be updated.
    ...    -${KeyValuePair}: This field specifies the variable/s that will be updated in a key-value format.
    ...    -${ProjectName}: This field specifies the project name in which the resources environment variable need change
    ...    -${AdvancedOption}: This field specifies any other additional options which will be used during the action execution.
    Generate String
    Init Action    ${SET_ENV_VARIABLE}
    Connect To OpenShift
    Run Keyword If    '${ResourceType}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_RESOURCE_TYPE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_RESOURCE_TYPE#    ${ResourceType}
    Run Keyword If    '${ResourceName}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_RESOURCE_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_RESOURCE_NAME#    ${ResourceName}
    Run Keyword If    '${KeyValuePair}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_KEY_VALUE_PAIR#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_KEY_VALUE_PAIR#    ${KeyValuePair}
    Run Keyword If    '${ProjectName}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_PROJECT#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_PROJECT#    ${ProjectName}
    Run Keyword If    '${AdvancedOption}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_ADV_OPTS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_ADV_OPTS#    ${AdvancedOption}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "SET ENVIRONMENT VARIABLE"*******
