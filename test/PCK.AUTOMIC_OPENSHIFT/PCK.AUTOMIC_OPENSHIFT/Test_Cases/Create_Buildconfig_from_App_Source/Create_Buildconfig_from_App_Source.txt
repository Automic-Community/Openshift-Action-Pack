*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        create_buildconfig_from_app_source
Test Template     Create Buildconfig from App Source Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ApplicationSource            ProjectName        AdvancedOption
Create Buildconfig from App Source action with valid values passed in all fields
                      [Tags]    main_scenarios
                      OK        ${Application_Source_URL}    ${Project_Name}    ${Srvc_Advanced_Options}

Create Buildconfig action with Invalid Application Source
                      NOK       Invaid                       ${EMPTY}           ${EMPTY}

Create Buildconfig from App Source action with Invalid Project Name
                      NOK       ${Application_Source_URL}    Invalid            ${EMPTY}

Create Buildconfig from App Source action with Invalid advanced option
                      NOK       ${Application_Source_URL}    ${Project_Name}    Invalid

Assert Application Source as Mandatory Field
                      NOK       ${EMPTY}                     ${Project_Name}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "CREATE BUILDCONFIG FROM APP SOURCE"*******
    Log    *******Connect to AE*******
    Connect AE

Create Buildconfig from App Source Template
    [Arguments]    ${OK_NOK}    ${ApplicationSource}    ${ProjectName}    ${AdvancedOption}
    [Documentation]    This test creates a buildconfig using application source
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ApplicationSource}: This field specifies the URL or local path to application source.
    ...    -${ProjectName}: This field specifies the project name for which you want to create buildconfig
    ...    -${AdvancedOption}: This field specifies any other additional options which will be used during creation of buildconfig.
    Generate String
    Init Action    ${CREATE_BUILDCONFIG_FROM_APP_SOURCE}
    Connect To OpenShift
    Run Keyword If    '${ApplicationSource}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_APP_URL#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_APP_URL#    ${ApplicationSource}
    Run Keyword If    '${AdvancedOption}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_ADV_OPTS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_ADV_OPTS#    ${AdvancedOption}
    Run Keyword If    '${ProjectName}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_PROJECT#    ${Generated_Name2}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_PROJECT#    ${ProjectName}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "CREATE BUILDCONFIG FROM APP SOURCE"*******
