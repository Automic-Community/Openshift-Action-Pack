*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        create_app_from_template
Test Template     Create Application from Template Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Template                 TemplateSource               AppName      Project            AdvancedOption
Create Application using OpenShift template and providing application name and advanced options
                      [Tags]    main_scenarios
                      OK        ${OpenShift_Template}    OpenShift stored template    Generated    ${EMPTY}           ${Advanced_Options}

Create Application using Local Template File and providing Project
                      OK        ${Local_Template}        Local template file          ${EMPTY}     ${Project_Name}    ${EMPTY}

Create Application with Invalid Template Name
                      NOK       Invalid                  Local template file          ${EMPTY}     ${EMPTY}           ${EMPTY}

Create Application with Invalid Application Name using uppercase and special characters
                      NOK       ${Local_Template}        Local template file          APP!         ${EMPTY}           ${EMPTY}

Create Application with Invalid Application Name exceeding 58 characters
                      NOK       ${Local_Template}        Local template file          Invalid      ${EMPTY}           ${EMPTY}

Create Application with Non-existing Project Name
                      NOK       ${Local_Template}        Local template file          ${EMPTY}     Invalid            ${EMPTY}

Create Application with Invalid advanced option
                      NOK       ${Local_Template}        Local template file          ${EMPTY}     ${EMPTY}           Invalid

Assert Template as Mandatory Field
                      NOK       ${EMPTY}                 Local template file          Generated    ${Project_Name}    ${Advanced_Options}

*** Keywords ***
Startup
    Log    *******Start Test "CREATE APPLICATION FROM TEMPLATE"*******
    Log    *******Connect to AE*******
    Connect AE

Create Application from Template Template
    [Arguments]    ${OK_NOK}    ${Template}    ${TemplateSource}    ${AppName}    ${Project}    ${AdvancedOption}
    [Documentation]    This test creates a application from template in OpenShift or local template file.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${Template}: This field specifies template specification.
    ...    -${TemplateSource}: This field specifies openshift stored template or local template file.
    ...    -${AppName}: This field specifies application name to be provided to new application
    ...    -${Project}: This field specifies the project name in which you want to create application
    ...    -${AdvancedOption}: This field specifies any other additional options which will be used during the application creation.
    Generate String
    Init Action    ${CREATE_APPLICATION_FROM_TEMPLATE}
    Connect To OpenShift
    Run Keyword If    '${Template}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_TEMPLATE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_TEMPLATE#    ${Template}
    Action Set    &UC4RB_OPENSHIFT_TMPL_SRC#    ${TemplateSource}
    Run Keyword If    '${AdvancedOption}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_ADV_OPTS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_ADV_OPTS#    ${AdvancedOption}
    Run Keyword If    '${AppName}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_APP_NAME#    ${Generated_Name2}_${Time}    ELSE IF    '${AppName}' == 'Generated'
    ...    Action Set    &UC4RB_OPENSHIFT_APP_NAME#    ${Generated_Name}    ELSE    Action Set    &UC4RB_OPENSHIFT_APP_NAME#
    ...    ${AppName}
    Run Keyword If    '${Project}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_PROJECT#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_PROJECT#    ${Project}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "CREATE APPLICATION FROM TEMPLATE"*******
