*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        create_buildconfig_from_dockerfile
Test Template     Create Buildconfig from Dockerfile Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    DockerfileContents        ProjectName        AdvancedOption
Create Buildconfig from Dockerfile action with valid values passed in all fields
                      [Tags]    main_scenarios
                      OK        ${Dockerfile_Contents}    ${Project_Name}    ${Srvc_Advanced_Options}

Create Buildconfig action with Invalid Dockerfile Contents
                      NOK       Invaid                    ${EMPTY}           ${EMPTY}

Create Buildconfig from Dockerfile action with Invalid Project Name
                      NOK       ${Dockerfile_Contents}    Invalid            ${EMPTY}

Create Buildconfig from Dockerfile action with Invalid advanced option
                      NOK       ${Dockerfile_Contents}    ${Project_Name}    Invalid

Assert Dockerfile Contents as Mandatory Field
                      NOK       ${EMPTY}                  ${Project_Name}    ${Srvc_Advanced_Options}

*** Keywords ***
Startup
    Log    *******Start Test "CREATE BUILDCONFIG FROM DOCKERFILE"*******
    Log    *******Connect to AE*******
    Connect AE

Create Buildconfig from Dockerfile Template
    [Arguments]    ${OK_NOK}    ${DockerfileContents}    ${ProjectName}    ${AdvancedOption}
    [Documentation]    This test creates a buildconfig using dockerfile
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${DockerfileContents}: This field specifies the contents of dockerfile.
    ...    -${ProjectName}: This field specifies the project name for which you want to create buildconfig
    ...    -${AdvancedOption}: This field specifies any other additional options which will be used during creation of buildconfig.
    Generate String
    Init Action    ${CREATE_BUILDCONFIG_FROM_DOCKERFILE}
    Connect To OpenShift
    Run Keyword If    '${DockerfileContents}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_DOCKERFILE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_DOCKERFILE#    ${DockerfileContents}
    Run Keyword If    '${AdvancedOption}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_ADV_OPTS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_ADV_OPTS#    ${AdvancedOption}
    Run Keyword If    '${ProjectName}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_PROJECT#    ${Generated_Name2}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_PROJECT#    ${ProjectName}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "CREATE BUILDCONFIG FROM DOCKERFILE"*******
