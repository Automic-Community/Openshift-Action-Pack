*** Test Cases ***    OK_NOK    ResourceType        ResourceName        Keys                ProjectName
Unset Environment Variable action with valid values passed in all fields for resource type buildconfig
                      [Tags]    main_scenarios
                      OK        buildconfig         ${Resource_Name}    ${Single_Key}       ${Project_Name}

Unset Environment Variable action with valid values passed in all fields for resource type deploymentconfig
                      [Tags]    main_scenarios
                      OK        deploymentconfig    ${Resource_Name}    ${Single_Key}       ${Project_Name}

Unset Environment Variable action with valid multiple keys while leaving Project field as empty
                      OK        buildconfig         ${Resource_Name}    ${Multiple_Keys}    ${EMPTY}

Unset Environment Variable action with unexistent/previously unset key but passed in correct key format
                      OK        buildconfig         ${Resource_Name}    ${Single_Key}       ${EMPTY}

Mandatory assertion test for Unset Environment Variable when ResourceName is not provided
                      NOK       buildconfig         ${EMPTY}            ${Multiple_Keys}    ${EMPTY}

Mandatory assertion test for Unset Environment Variable when Key is not provided
                      NOK       buildconfig         ${Resource_Name}    ${EMPTY}            ${EMPTY}

Unset Environment Variable action with Invalid Resource name
                      NOK       deploymentconfig    Invalid             ${Single_Key}       ${EMPTY}

Unset Environment Variable action with single Key as input in incorrect format
                      NOK       deploymentconfig    ${Resource_Name}    Key-1               ${EMPTY}

Unset Environment Variable action with multiple keys as input where atleast one key-value in incorrect format
                      NOK       deploymentconfig    ${Resource_Name}    key_1,key+2         ${EMPTY}

Unset Environment Variable action with Invalid Project
                      NOK       buildconfig         ${Resource_Name}    ${Single_Key}       Invalid

*** Keywords ***
Startup
    Log    *******Start Test "UNSET ENVIRONMENT VARIABLE"*******
    Log    *******Connect to AE*******
    Connect AE

Unset Environment Template
    [Arguments]    ${OK_NOK}    ${ResourceType}    ${ResourceName}    ${Keys}    ${ProjectName}
    [Documentation]    This test exposes a services in the project
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ResourceType}: This field specifies resource type for which variable/s will be create or updated
    ...    -${ResourceName}: This field specifies the name of selected resource type in which environment variable/s will be updated.
    ...    -${Keys}: This field specifies the variable/s that will be updated in a key-value format.
    ...    -${ProjectName}: This field specifies the project name in which the resources environment variables need change
    Generate String
    Init Action    ${UNSET_ENV_VARIABLE}
    Connect To OpenShift
    Run Keyword If    '${ResourceType}' == 'Invalid'    Action Unset    &UC4RB_OPENSHIFT_RESOURCE_TYPE#    ${Generated_Name}_${Time}    ELSE    Action Unset
    ...    &UC4RB_OPENSHIFT_RESOURCE_TYPE#    ${ResourceType}
    Run Keyword If    '${ResourceName}' == 'Invalid'    Action Unset    &UC4RB_OPENSHIFT_RESOURCE_NAME#    ${Generated_Name}_${Time}    ELSE    Action Unset
    ...    &UC4RB_OPENSHIFT_RESOURCE_NAME#    ${ResourceName}
    Run Keyword If    '${Keys}' == 'Invalid'    Action Unset    &UC4RB_OPENSHIFT_KEYS#    ${Generated_Name}_${Time}    ELSE    Action Unset
    ...    &UC4RB_OPENSHIFT_KEYS#    ${Keys}
    Run Keyword If    '${ProjectName}' == 'Invalid'    Action Unset    &UC4RB_OPENSHIFT_PROJECT#    ${Generated_Name}_${Time}    ELSE    Action Unset
    ...    &UC4RB_OPENSHIFT_PROJECT#    ${ProjectName}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "UNSET ENVIRONMENT VARIABLE"*******
