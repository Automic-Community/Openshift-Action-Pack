*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        link_secret
Test Template     Link Secret Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ServiceAccountName     SecretName        SecretUse     ProjectName
Link Secret action with valid values passed in all fields and secret use as pull
                      [Tags]    main_scenarios
                      OK        ${Service_Acc_Name}    ${Secret_Name}    pull          ${Project_Name}

Link Secret action with valid values passed in all fields and secret use as pull and mount
                      [Tags]    main_scenarios
                      OK        ${Service_Acc_Name}    ${Secret_Name}    mount;pull    ${Project_Name}

Link Secret action with valid values passed in only mandatory fields
                      OK        ${Service_Acc_Name}    ${Secret_Name}    pull          ${EMPTY}

Mandatory assertion test for Link Secret when Service Account Name is not provided
                      NOK       ${EMPTY}               ${Secret_Name}    mount         ${Project_Name}

Mandatory assertion test for Link Secret when Secret Name is not provided
                      NOK       ${Service_Acc_Name}    ${EMPTY}          mount         ${Project_Name}

Link Secret action with invalid service account name
                      NOK       Invalid                ${Secret_Name}    pull          ${EMPTY}

Link Secret action with invalid Secret name
                      NOK       ${Service_Acc_Name}    Invalid           mount         ${EMPTY}

Link Secret action with invalid/non-existent Project Name
                      NOK       ${Service_Acc_Name}    ${Secret_Name}    pull          Invalid

*** Keywords ***
Startup
    Log    *******Start Test "LINK SECRET"*******
    Log    *******Connect to AE*******
    Connect AE

Link Secret Template
    [Arguments]    ${OK_NOK}    ${ServiceAccountName}    ${SecretName}    ${SecretUse}    ${ProjectName}
    [Documentation]    This test links a secret file with the specified service account
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ServiceAccountName}: This field specifies the service account to which a secret has to be linked
    ...    -${SecretName}: This field specifies the secret file that is to be linked to a service account
    ...    -${SecretUse}: This field specifies the use of the secret file, i.e. for mount or pull
    ...    -${ProjectName}: This field specifies the project name in which the link secret action has to be performed
    Generate String
    Init Action    ${LINK_SECRET}
    Connect To OpenShift
    Run Keyword If    '${ServiceAccountName}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_SERVICE_ACC#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_SERVICE_ACC#    ${ServiceAccountName}
    Run Keyword If    '${SecretName}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_SECRET_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_SECRET_NAME#    ${SecretName}
    Run Keyword If    '${ProjectName}' == 'Invalid'    Action Set    &UC4RB_OPENSHIFT_PROJECT#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_OPENSHIFT_PROJECT#    ${ProjectName}
    Action Set    &UC4RB_OPENSHIFT_SECRET_USE#    ${SecretUse}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "LINK SECRET"*******
